apply plugin: 'com.android.application'
apply plugin: 'jacoco'
apply plugin: 'org.sonarqube'

def androidSupportVersion = '23.3.0'

def rxandroidVersion = '1.2.0'
def eventbusVersion = "3.0.0"
def advancedrecyclerviewVersion = "0.9.1"

def junitVersion = "4.12"
def junitDataProviderVersion = "1.10.3"
def robolectricVersion = "3.0"
def mockitoVersion = "1.10.19"

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.3"

    defaultConfig {
        applicationId "me.raatiniemi.worker"
        minSdkVersion 21
        targetSdkVersion 23
        versionCode 16
        versionName "0.6.2"
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    // AndroidHttpClient was removed from the SDK in v23 of the build tools.
    // This causes issues since robolectric is running against an earlier
    // version of the SDK, and expects the class to be present.
    useLibrary 'org.apache.http.legacy'
}

buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath 'org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:2.0.1'
    }
}

tasks.sonarqube.dependsOn 'testReleaseUnitTest'

sonarqube {
    // noinspection GroovyAssignabilityCheck
    properties {
        def binaries = "${buildDir}/intermediates/classes/release"
        def androidPath = android.sdkDirectory.getPath()

        // noinspection GroovyAssignabilityCheck
        def libraries = "${buildDir}/intermediates/exploded-aar/**/classes.jar," +
                "${androidPath}/platforms/${android.compileSdkVersion}/android.jar," +
                "${androidPath}/extras/**/*.jar"

        property 'sonar.projectKey', 'worker:android'
        property 'sonar.projectName', 'worker'
        property 'sonar.projectVersion', android.defaultConfig.versionName

        property "sonar.sourceEncoding", "UTF-8"
        property "sonar.sources", "src/main/java,src/main/res"
        property "sonar.binaries", binaries
        property "sonar.libraries", libraries
        property "sonar.java.binaries", binaries
        property "sonar.java.libraries", libraries

        property "sonar.tests", "src/test/java"
        property "sonar.java.test.binaries", binaries
        property "sonar.java.test.libraries", libraries

        property 'sonar.java.source', android.compileOptions.sourceCompatibility
        property 'sonar.java.target', android.compileOptions.targetCompatibility

        property "sonar.scm.provider", "git"

        property "sonar.java.coveragePlugin", "jacoco"
        property "sonar.jacoco.reportPath", "${buildDir}/jacoco/testReleaseUnitTest.exec"
        property "sonar.junit.reportsPath", "${buildDir}/test-results/release"
    }
}

jacoco {
    toolVersion = "0.7.6.201602180812"
}

dependencies {
    compile "com.android.support:recyclerview-v7:${androidSupportVersion}"
    compile "com.android.support:cardview-v7:${androidSupportVersion}"
    compile "com.android.support:appcompat-v7:${androidSupportVersion}"
    compile "com.android.support:design:${androidSupportVersion}"

    compile "io.reactivex:rxandroid:${rxandroidVersion}"

    compile "org.greenrobot:eventbus:${eventbusVersion}"
    compile "com.h6ah4i.android.widget.advrecyclerview:advrecyclerview:${advancedrecyclerviewVersion}"

    testCompile "junit:junit:${junitVersion}"
    testCompile "com.tngtech.java:junit-dataprovider:${junitDataProviderVersion}"
    testCompile "org.robolectric:robolectric:${robolectricVersion}"
    testCompile "org.mockito:mockito-core:${mockitoVersion}"
}
